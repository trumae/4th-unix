\ Preprocessor FOOS bridge pattern demo - Copyright 2017 J.L. Bezemer
\ You can redistribute this file and/or modify it under
\ the terms of the GNU General Public License

include lib/fp4.4th
include lib/memcell.4th
include 4pp/lib/foos.4pp
include 4pp/lib/float.4pp

:: DrawingAPI
   class
     virtual: drawCircle
   end-class {}
;

:: DrawingAPI1
   extends DrawingAPI
   end-extends {

     :virtual drawCircle {
       ." API1.circle at " 0 .r ." :". ." radius " f. cr
     } ;method
   }
;

:: DrawingAPI2
   extends DrawingAPI
   end-extends {

     :virtual drawCircle {
       ." API2.circle at " 0 .r ." :". ." radius " f. cr
     } ;method
   }
;

:: Shape
   class
     field: API
     virtual: draw
     virtual: resizeByPercentage
   end-class {

     this -> API !
   }
;

:: CircleShape
   extends Shape
     field: x
     field: y
     ffield: radius
   end-extends {

     this -> radius f!
     this -> y !
     this -> x !

     :virtual draw {
        this -> radius f@ this -> y @ this -> x @ this -> API @ => drawCircle
     } ;method

     :virtual resizeByPercentage { this -> radius dup f@ f* f! } ;method
   }

   private{ x y radius }
;

fclear

1 3 f%  7 new DrawingAPI1 new CircleShape
5 7 f% 11 new DrawingAPI2 new CircleShape swap

dup f% 2.5e => resizeByPercentage => draw
dup f% 2.5e => resizeByPercentage => draw
